# Maintainer: mar77i <mysatyre at gmail dot com>
# Past Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Scytrin dai Kinthra <scytrin@gmail.com>

pkgname=st
pkgver=0.7.7.g68bae9c
pkgrel=1
pkgdesc='Simple virtual terminal emulator for X'
url='http://git.suckless.org/st/'
arch=('i686' 'x86_64')
license=('MIT')
depends=('libxft')
makedepends=('ncurses' 'libxext' 'git')
source=("$pkgname::git+http://git.suckless.org/st#commit=68bae9c7b121e30114f6b0cabe15da3fae46e673"
        config.h
	st-alpha-20160727-308bfbf.diff
	st-externalpipe-20160727-308bfbf.diff
	st-hidecursor-20160727-308bfbf.diff
	st-scrollback-20160924-f739843.diff
	)
md5sums=('SKIP'
	'df92ccf33454349ce51252b7eff74fbc'
	'037bb7f2a354fd9e3c15b4aedd103165'
	'47d628501defd776efd0d4fbb1968895'
	'8ff8a77b34dfc09a4dd0d2cf876d68e7'
	'f6a780d3e759f577d2fff7a9350b8655')

pkgver() {
	cd "${srcdir}/${pkgname}"
	git describe --tags |sed 's/-/./g'
}

prepare() {
	cd "$pkgname"
	git apply $srcdir/st-alpha-20160727-308bfbf.diff
	# Git can't deal with the offsets, won't apply
	patch -p1 < $srcdir/st-externalpipe-20160727-308bfbf.diff
	git apply $srcdir/st-hidecursor-20160727-308bfbf.diff
	git apply $srcdir/st-scrollback-20160924-f739843.diff
	cp $srcdir/config.h config.h
}

build() {
	cd "${srcdir}/${pkgname}"
	make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
	cd "${srcdir}/${pkgname}"
	make PREFIX=/usr DESTDIR="${pkgdir}" install
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
}
