# Contributor: Patrick Jackson <PatrickSJackson gmail com>
# Maintainer: Christoph Vigano <mail@cvigano.de>

pkgname=st-personalized
_pkgname=st
pkgver=0.6
pkgrel=1
pkgdesc='A simple virtual terminal emulator for X.'
arch=('i686' 'x86_64')
license=('MIT')
depends=('libxft')
makedepends=('ncurses')
url="http://st.suckless.org"
source=(http://dl.suckless.org/st/$_pkgname-$pkgver.tar.gz
        config.h
	1clipboard.diff
	class_mod.diff
	st-0.6-hidexcursor.diff
	st-git-20150601-copyurl.diff)

provides=("$_pkgname")
conflicts=("$_pkgname")

prepare() {
	cd "$srcdir/$_pkgname-$pkgver"
	patch -i $srcdir/1clipboard.diff
	patch -i $srcdir/class_mod.diff
	patch -i $srcdir/st-0.6-hidexcursor.diff
	patch -i $srcdir/st-git-20150601-copyurl.diff
}

build() {
  cd $srcdir/$_pkgname-$pkgver
  cp $srcdir/config.h config.h
	make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd $srcdir/$_pkgname-$pkgver
  make PREFIX=/usr DESTDIR="$pkgdir" TERMINFO="$pkgdir/usr/share/terminfo" install
	install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
	install -Dm644 README "$pkgdir/usr/share/doc/$pkgname/README"
}
md5sums=('1a926f450b4eacb7e2f5ac5b8ffea7c8'
	'09de2252d5b12d740a1daef37086acd8'
	'72545974bee86c0ef9bc7e67c652031d'
	'c1131be748fa8f5c03897dfd6cdf5e0a'
	'fd30c59d587f91a1dd5f82cd2ceb752c'
	'aff553417f6ac0d74fe79b468b74c0f9')
