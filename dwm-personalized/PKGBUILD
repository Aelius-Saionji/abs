pkgname=dwm-personalized
_pkgname=dwm
pkgver=6.0.43.g14343e6
pkgver(){
  cd $_pkgname
  git describe --tags |sed 's/-/./g'
}
pkgrel=1

pkgdesc="A dynamic window manager for X"
url="http://dwm.suckless.org"
arch=('i686' 'x86_64')
license=('MIT')
options=(zipman)
depends=('libx11' 'libxinerama')
optdepends=('xbacklight' 'qiv' 'st')
makedepends=('git')
install=dwm.install
provides=('dwm')
conflicts=('dwm')
source=(config.h
	class.patch
	dwm.desktop
	"$_pkgname::git+http://git.suckless.org/dwm"
	dwm-6.0-bstack.diff
	dwm-6.0-emptyview.diff
	dwm-6.1-cfacts.diff
	dwm-6.1-gaplessgrid.diff
	dwm-6.1-hide_vacant_tags.diff
	dwm-6.1-moveresize.diff
	dwm-6.1-push.diff
	dwm-6.1-runorraise-fixed.diff
	dwm-6.1-save_floats.diff)
md5sums=('75ac52dd2592642e670b80221a47a252'
	 '091126ed1734848375d756bf0909f083'
	 '939f403a71b6e85261d09fc3412269ee'
         'SKIP'
         'b60183f343d3912e841d5cbda3de768f'
         '642a7f40ca61702e56458b69c6a92bcb'
         '252cfab251e876b9539bf0c4d0db5aa7'
         'a298c7e6e69134f24bca88016e3463ce'
	 'cb3c56c8fddbe0ba4fb66596f04abe9a'
         '6ba0b950da1a837fa3e302f66f4e0a1a'
	 '3088dcea71d1a08fac760f88a9f2cec0'
         '265c6cf54d1ab09ec4f784f57a4b82d9'
         'a12ce4b78a6906c4f0ad67edfd46399a')

prepare() {
  cd $_pkgname
  patch -F3 -p1 < $srcdir/class.patch
  patch -F3 -p1 < $srcdir/dwm-6.0-bstack.diff
  patch -F3 -p1 < $srcdir/dwm-6.0-emptyview.diff
  patch -p1 < $srcdir/dwm-6.1-cfacts.diff
  patch -p1 < $srcdir/dwm-6.1-gaplessgrid.diff
  patch -p1 < $srcdir/dwm-6.1-hide_vacant_tags.diff
  patch -p1 < $srcdir/dwm-6.1-moveresize.diff
  patch -p1 < $srcdir/dwm-6.1-push.diff
  patch -p1 < $srcdir/dwm-6.1-runorraise-fixed.diff
  patch -p1 < $srcdir/dwm-6.1-save_floats.diff
}

build() {
  cd $_pkgname
  cp $srcdir/config.h config.h
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  make -C $_pkgname PREFIX=/usr DESTDIR=$pkgdir install
  install -m644 -D $_pkgname/LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
  install -m644 -D $_pkgname/README $pkgdir/usr/share/doc/$pkgname/README
  install -m644 -D $srcdir/dwm.desktop $pkgdir/usr/share/xsessions/dwm.desktop
}
