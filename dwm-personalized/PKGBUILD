# $Id: PKGBUILD 154170 2015-12-22 09:55:49Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Dag Odenhall <dag.odenhall@gmail.com>
# Contributor: Grigorios Bouzakis <grbzks@gmail.com>
pkgname=dwm-personalized
_pkgname=dwm
pkgver=6.1.2.g3465bed
pkgver(){
  cd $_pkgname
  git describe --tags |sed 's/-/./g'
}
pkgrel=1
pkgdesc="A dynamic window manager for X"
url="http://dwm.suckless.org"
arch=('i686' 'x86_64')
license=('MIT')
options=(zipman)
depends=('libx11' 'libxinerama' 'libxft' 'freetype2' 'st')
optdepends=('xbacklight' 'qiv' 'st')
makedepends=('git')
install=dwm.install
provides=('dwm')
conflicts=('dwm')
source=("$_pkgname::git+http://git.suckless.org/dwm#commit=3465bed290abc62cb2e69a8096084ba6b8eb4956"
	class.patch
	config.h
	dwm.desktop
	emptyview-fixed.diff)
md5sums=('SKIP'
	 '091126ed1734848375d756bf0909f083'
	 'febd3bc4ca8f9fa5d639a622dd491268'
	 '939f403a71b6e85261d09fc3412269ee'
	 '40565453fa4179b6ec5f1f3b69c20a97')

prepare() {
  cd $_pkgname
  git apply $srcdir/emptyview-fixed.diff
#  patch -F3 -p1 < $srcdir/class.patch
  cp $srcdir/config.h config.h
}

build() {
  cd $_pkgname
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11 FREETYPEINC=/usr/include/freetype2
}

package() {
  cd $_pkgname
  make PREFIX=/usr DESTDIR=$pkgdir install
  install -m644 -D LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
  install -m644 -D README $pkgdir/usr/share/doc/$pkgname/README
  install -m644 -D $srcdir/dwm.desktop $pkgdir/usr/share/xsessions/dwm.desktop
}
