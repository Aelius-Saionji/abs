pkgname=dwm-git
_pkgname=dwm
pkgver=6.2
pkgrel=1
pkgdesc="A dynamic window manager for X"
url="http://dwm.suckless.org"
arch=('i686' 'x86_64')
license=('MIT')
options=(zipman)
depends=('libx11' 'libxinerama' 'libxft')
makedepends=('git')
provides=('dwm')
conflicts=('dwm')
epoch=1
source=('git://git.suckless.org/dwm#commit=cb3f58ad06993f7ef3a7d8f61468012e2b786cab'
	'config.h'
	'columns-include.diff'
	'deck-include.diff'
	'dmenu-env.diff'
	'dmenumon-for-all.diff'
	'dwm-6.1-horizgrid-fixed.diff'
	'dwm-6.1-resizecorners.diff'
	'dwm-bottomstack-20160719-56a31dc-include.diff'
	'dwm-gaplessgrid-20160731-56a31dc.diff'
	'dwm-pertag-tab-combo.diff'
	'dwm-savefloats-20181212-b69c870.diff'
	'emptyview-fixed.diff'
	'dwm.desktop'
	'xinitrc.desktop'
	'exec-xinitrc')
md5sums=('SKIP'
         '30c5a65589afe2190f17659bab46d951'
         '9f00d621857ec9fbaebaaf963429a7fc'
         '7dbe956cd0b4ab3376cfbdf4423ec1c4'
         '0628cd5b24af25790800c8c0ef21ce9f'
         'aac97b9fe68c14725bc49e8a70cfc436'
         '828ea644a0d8ab3855bf095466f05533'
         '5cfdb08e42746092d825284669dd10ab'
         '023c17c902150f58e524e295112b11d1'
         '97b7cc31611250e7e71484f1b423f03e'
         '19a099cb5f89f665f9e6119f10fcaefe'
         'c2576b5b80958e76525473260bf09cc9'
         '40565453fa4179b6ec5f1f3b69c20a97'
         '939f403a71b6e85261d09fc3412269ee'
         '82467a0c7ec91bce9d1295a633a6ee31'
         '329e3590082a05e835b37f23d111fc72')

pkgver() {
	cd $_pkgname
	git describe --tags |sed 's/-/./g'
}

prepare() {
	cd $_pkgname
	git apply --reject --ignore-space-change $srcdir/columns-include.diff # manually add #include "column.c" and the layout in config.h
	git apply --reject --ignore-space-change $srcdir/deck-include.diff # manually add #include "deck.c" and the layout in config.h
	git apply --reject --ignore-space-change $srcdir/dmenu-env.diff
	git apply --reject --ignore-space-change $srcdir/dmenumon-for-all.diff
	git apply --reject --ignore-space-change $srcdir/dwm-6.1-horizgrid-fixed.diff # manually add #include "horizgrid.c" and the layout in config.h
	git apply --reject --ignore-space-change $srcdir/dwm-6.1-resizecorners.diff
	git apply --reject --ignore-space-change $srcdir/dwm-bottomstack-20160719-56a31dc-include.diff # manually add #include "bstack.c" and the two layouts bstack bstackhoriz in config.h
	git apply --reject --ignore-space-change $srcdir/dwm-gaplessgrid-20160731-56a31dc.diff # manually add #include "gaplessgrid.c" and the layout in config.h
	git apply --reject --ignore-space-change $srcdir/dwm-pertag-tab-combo.diff
	git apply --reject --ignore-space-change $srcdir/dwm-savefloats-20181212-b69c870.diff
	#git apply --reject --ignore-space-change $srcdir/emptyview-fixed.diff # broken by dwm-pertag-tab-combo.diff 
	cp $srcdir/config.h config.h
}

build() {
	cd $_pkgname
	make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
	cd $_pkgname
	make PREFIX=/usr DESTDIR="$pkgdir" install
	install -m644 -D LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
	install -m644 -D README "$pkgdir/usr/share/doc/$pkgname/README"
	install -m644 -D ../dwm.desktop "$pkgdir/usr/share/xsessions/dwm.desktop"
	install -m644 -D ../xinitrc.desktop "$pkgdir/usr/share/xsessions/xinitrc.desktop"
	install -m755 -D ../exec-xinitrc "$pkgdir/usr/bin/exec-xinitrc"
}
