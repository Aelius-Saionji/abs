pkgname=dwm-git
_pkgname=dwm
pkgver=6.1.18.gceac8c9
pkgrel=1
pkgdesc="A dynamic window manager for X"
url="http://dwm.suckless.org"
arch=('i686' 'x86_64')
license=('MIT')
options=(zipman)
depends=('libx11' 'libxinerama' 'libxft')
makedepends=('git')
provides=('dwm')
conflicts=('dwm')
epoch=1
source=('git://git.suckless.org/dwm#commit=ceac8c91ff3bf45ae53135658d6f560cb2335133'
	'config.h'
	'columns-include.diff'
	'deck-include.diff'
	'dmenu-env.diff'
	'dwm-6.1-horizgrid-fixed.diff'
	'dwm-6.1-resizecorners.diff'
	'dwm-bottomstack-20160719-56a31dc-include.diff'
	'dwm-gaplessgrid-20160731-56a31dc.diff'
	'dwm-savefloats-20160723-56a31dc.diff'
	'dwm.desktop'
	'emptyview-fixed.diff'
	'dmenumon-for-all.diff')
md5sums=('SKIP'
         '100f6bf9ee10402c0f5ad81df4cc2c89'
         '9f00d621857ec9fbaebaaf963429a7fc'
         '7dbe956cd0b4ab3376cfbdf4423ec1c4'
         '5145ebd0e38226eb8fe32776efca7a19'
         '828ea644a0d8ab3855bf095466f05533'
         '5cfdb08e42746092d825284669dd10ab'
         '023c17c902150f58e524e295112b11d1'
         '97b7cc31611250e7e71484f1b423f03e'
         '103805203b6f34cfcba7477d57bd98f2'
         '939f403a71b6e85261d09fc3412269ee'
         '40565453fa4179b6ec5f1f3b69c20a97'
         '4909e70765c9d3174e763d59e2adb5f4')

pkgver() {
	cd $_pkgname
	git describe --tags |sed 's/-/./g'
}

prepare() {
	cd $_pkgname
	git apply --ignore-space-change $srcdir/columns-include.diff # manually add #include "column.c" and the layout in config.h
	git apply --ignore-space-change $srcdir/deck-include.diff # manually add #include "deck.c" and the layout in config.h
	git apply --ignore-space-change $srcdir/dwm-6.1-horizgrid-fixed.diff # manually add #include "horizgrid.c" and the layout in config.h
	git apply --ignore-space-change $srcdir/dwm-6.1-resizecorners.diff
	git apply --ignore-space-change $srcdir/dwm-bottomstack-20160719-56a31dc-include.diff # manually add #include "bstack.c" and the two layouts bstack bstackhoriz in config.h
	git apply --ignore-space-change $srcdir/dwm-gaplessgrid-20160731-56a31dc.diff # manually add #include "gaplessgrid.c" and the layout in config.h
	git apply --ignore-space-change $srcdir/dwm-savefloats-20160723-56a31dc.diff
	git apply --ignore-space-change $srcdir/dmenu-env.diff
	git apply --ignore-space-change $srcdir/dmenumon-for-all.diff
	git apply --ignore-space-change $srcdir/emptyview-fixed.diff
	cp $srcdir/config.h config.h
}

build() {
	cd $_pkgname
	make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
	cd $_pkgname
	make PREFIX=/usr DESTDIR="$pkgdir" install
	install -m644 -D LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
	install -m644 -D README "$pkgdir/usr/share/doc/$pkgname/README"
	install -m644 -D ../dwm.desktop "$pkgdir/usr/share/xsessions/dwm.desktop"
}
