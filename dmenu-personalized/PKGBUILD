# $Id: PKGBUILD 106204 2014-02-25 11:21:42Z bpiotrowski $
# Maintainer:  Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Thorsten Töpper <atsutane-tu@freethoughts.de>
# Contributor: Thayer Williams <thayer@archlinux.org>
# Contributor: Jeff 'codemac' Mickey <jeff@archlinux.org>

pkgname=dmenu-personalized
_pkgname=dmenu
pkgver=4.5.13.5ed5e90
pkgrel=1
pkgdesc="A generic menu for X"
url="http://tools.suckless.org/dmenu/"
arch=('i686' 'x86_64')
license=('MIT')
depends=('sh' 'libxinerama')
makedepends=('git')
provides=('dmenu')
conflicts=('dmenu')
source=(git://git.suckless.org/dmenu#commit=5ed5e90bf
	class.patch
	shift-insert.patch
	better-shift-insert.patch)
md5sums=('SKIP'
	 'dfa9eecdf73879fcfd8b04ff336bcb30'
	 'ae45c6bf23d69f138a6618c7389ef72c'
	 'dffce7fafd759df1185d98c7f79b2bd4')

pkgver() {
  cd $_pkgname
  echo 4.5.$(git rev-list 7f45b3f7a..@ --count).$(git rev-parse --short HEAD)
}

prepare() {
  cd $_pkgname
  patch -i $srcdir/class.patch
  patch -i $srcdir/better-shift-insert.patch
}

build(){
  cd $_pkgname
  make \
    X11INC=/usr/include/X11 \
    X11LIB=/usr/lib/X11
}

package() {
  cd $_pkgname
  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
