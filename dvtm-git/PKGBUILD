# Maintainer: Sean Haugh <seanphaugh@gmail.com>
# Contributor: Frank Lenormand <lenormf@gmail.com>
_pkgname=dvtm
_owner=martanne
pkgname=dvtm-git
pkgver=0.15.40.g311a8c0
pkgrel=1
pkgdesc='Dynamic virtual terminal manager'
arch=('i686' 'x86_64')
url="https://github.com/$_owner/$_pkgname"
license=('MIT')
depends=('sh')
makedepends=('git')
provides=("$_pkgname=$pkgver-$pkgrel")
conflicts=('dvtm')
source=("git+https://github.com/$_owner/$_pkgname"
	'plumb.diff')
md5sums=('SKIP'
         '57506ab3821d601b4b675b9653eb3fcd')

pkgver() {
	cd "$_pkgname"
	git describe --long | sed 's/^v//; s/-/./g'
}

prepare() {
	cd "$_pkgname"
	git apply --reject --ignore-space-change $srcdir/plumb.diff
}

build() {
	cd "$_pkgname"
	make
}

package() {
	cd "$_pkgname"
	make PREFIX=/usr DESTDIR="$pkgdir" install
	install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

	msg "Removing /usr/share/terminfo for compatibility with ncurses..."
	rm -rv "$pkgdir/usr/share/terminfo"
}
# vim: set ts=2 sw=2 noet:
