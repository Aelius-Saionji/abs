# $Id: PKGBUILD 128107 2015-02-22 11:00:35Z lfleischer $
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Giorgio Lando <patroclo7@gmail.com>
# Contributor: bender02 at gmx dot com

pkgname=dvtm
pkgver=0.14
pkgrel=1
pkgdesc='Dynamic virtual terminal manager.'
arch=('i686' 'x86_64')
url='http://www.brain-dump.org/projects/dvtm/'
license=('MIT')
depends=('ncurses')
source=("http://www.brain-dump.org/projects/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        'config.h'
	'idle-fifo.patch')
md5sums=('44ec5176b1996fdffd97cabe9a35ea0c'
         'cc29246822bec86fba2f44cb14130984'
	 'de35df8069fb7504f64ca15a35f3eae5')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	cp "${srcdir}/config.h" .
	sed -i 's/CFLAGS =/CFLAGS +=/' config.mk
	patch -i $srcdir/idle-fifo.patch
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make clean
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make PREFIX=/usr DESTDIR="${pkgdir}" install
	install -Dm0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
