# Maintainer: mar77i <mar77i at mar77i dot ch>
# Past Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Scytrin dai Kinthra <scytrin@gmail.com>

pkgname=st-git
_pkgname=st
pkgver=0.8.1.6.gdc3b5ba
pkgrel=1
pkgdesc='Simple virtual terminal emulator for X'
url='http://st.suckless.org/'
arch=('i686' 'x86_64')
license=('MIT')
options=('zipman')
depends=('libxft')
makedepends=('ncurses' 'libxext' 'git')
epoch=1
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=('git://git.suckless.org/st#commit=dc3b5babf1f8639a0d65cd347fc7879ac0461012'
        'config.h'
	'st-externalpipe-0.8.diff'
	'st-hidecursor-0.8.diff')
md5sums=('SKIP'
         'b91658b12a27edd190f478bcebe7ab4d'
         'ef7d8404215cb73e41c8fe63ff3356c5'
         'ede665482752bea51edb1ce8f0d135c8')

pkgver() {
	cd "${_pkgname}"
	git describe --tags |sed 's/-/./g'
}

prepare() {
	cd "${_pkgname}"
	patch -p1 < $srcdir/st-externalpipe-0.8.diff
	patch -p1 < $srcdir/st-hidecursor-0.8.diff
	cp $srcdir/config.h config.h
}

build() {
	cd "${_pkgname}"
	make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
	cd "${_pkgname}"
	make PREFIX=/usr DESTDIR="${pkgdir}" install
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
	#tic -s st.info -o "${pkgdir}/usr/share/terminfo"
}
