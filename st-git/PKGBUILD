# Maintainer: mar77i <mar77i at mar77i dot ch>
# Past Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Scytrin dai Kinthra <scytrin@gmail.com>

pkgname=st-git
_pkgname=st
pkgver=0.7.37.gb1338e9
pkgrel=1
pkgdesc='Simple virtual terminal emulator for X'
url='http://st.suckless.org/'
arch=('i686' 'x86_64')
license=('MIT')
options=('zipman')
depends=('libxft')
makedepends=('ncurses' 'libxext' 'git')
epoch=1
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=('git://git.suckless.org/st#commit=b1338e91ed632adbcd08388de37e46cf25326e01'
        'config.h'
	'st-externalpipe-20170608-b331da5.diff'
	'st-hidecursor-20170404-745c40f.diff')
md5sums=('SKIP'
         'c166b1b66a31be5c86e83a52a2d9de0f'
         '77e080a98bdc395f8d546841b02a0ffd'
         '487dc42b84ea464474aac527c2b47191')

pkgver() {
	cd "${_pkgname}"
	git describe --tags |sed 's/-/./g'
}

prepare() {
	cd "${_pkgname}"
	patch -p1 < $srcdir/st-externalpipe-20170608-b331da5.diff
	patch -p1 < $srcdir/st-hidecursor-20170404-745c40f.diff
	cp $srcdir/config.h config.h
}

build() {
	cd "${_pkgname}"
	make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
	cd "${_pkgname}"
	make PREFIX=/usr DESTDIR="${pkgdir}" install
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
	tic -s st.info -o "${pkgdir}/usr/share/terminfo"
}
