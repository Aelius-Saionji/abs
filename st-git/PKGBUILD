# Maintainer: mar77i <mar77i at mar77i dot ch>
# Past Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Scytrin dai Kinthra <scytrin@gmail.com>

pkgname=st-git
_pkgname=st
pkgver=0.7.17.gc63a87c
pkgrel=1
pkgdesc='Simple virtual terminal emulator for X'
url='http://st.suckless.org/'
arch=('i686' 'x86_64')
license=('MIT')
options=('zipman')
depends=('libxft')
makedepends=('ncurses' 'libxext' 'git')
epoch=1
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=('git://git.suckless.org/st#commit=c63a87cd936c1eeef14c4c21572e5b782d3df4bc'
        'config.h'
        'st-alpha-20160727-308bfbf.diff'
        'st-externalpipe-20160727-308bfbf.diff'
	'st-hidecursor-20160727-308bfbf.diff')
md5sums=('SKIP'
         'b5b2cf67e7dd5df306ac6e4833c1e197'
         '037bb7f2a354fd9e3c15b4aedd103165'
         '47d628501defd776efd0d4fbb1968895'
         '8ff8a77b34dfc09a4dd0d2cf876d68e7')

pkgver() {
	cd "${_pkgname}"
	git describe --tags |sed 's/-/./g'
}

prepare() {
	cd "${_pkgname}"
	git apply $srcdir/st-alpha-20160727-308bfbf.diff
	# Git can't deal with the offsets, won't apply
	patch -p1 < $srcdir/st-externalpipe-20160727-308bfbf.diff
	git apply $srcdir/st-hidecursor-20160727-308bfbf.diff
	cp $srcdir/config.h config.h
}

build() {
	cd "${_pkgname}"
	make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
	cd "${_pkgname}"
	make PREFIX=/usr DESTDIR="${pkgdir}" install
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
	tic -s st.info -o "${pkgdir}/usr/share/terminfo"
}
