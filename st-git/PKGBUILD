# Maintainer: mar77i <mar77i at mar77i dot ch>
# Past Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Scytrin dai Kinthra <scytrin@gmail.com>

pkgname=st-git
_pkgname=st
pkgver=0.8.2.1.ge85b6b6
pkgrel=1
pkgdesc='Simple virtual terminal emulator for X'
url='http://st.suckless.org/'
arch=('i686' 'x86_64')
license=('MIT')
options=('zipman')
depends=('libxft')
makedepends=('ncurses' 'libxext' 'git')
epoch=1
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=('git://git.suckless.org/st#commit=e85b6b64660214121164ea97fb098eaa4935f7db'
	'config.h'
	'plumb_without_shell_OSC.diff'
	'st-externalpipe-20181016-3be4cf1.diff'
	'st-hidecursor-0.8.1-fixed.diff')
md5sums=('SKIP'
         '7e277a1dbc60772ca982fec03bc547be'
         'c0a38eba884579b754a367212598447f'
         '68df469f840cf9bec54b617535ccea08'
         'e15b0da610486dbfe1ad271c3c8a6636')

pkgver() {
	cd "${_pkgname}"
	git describe --tags |sed 's/-/./g'
}

prepare() {
	cd "${_pkgname}"
	git apply --reject --ignore-space-change $srcdir/plumb_without_shell_OSC.diff
	git apply --reject --ignore-space-change $srcdir/st-externalpipe-20181016-3be4cf1.diff
	git apply --reject --ignore-space-change $srcdir/st-hidecursor-0.8.1-fixed.diff
	cp $srcdir/config.h config.h
}

build() {
	cd "${_pkgname}"
	make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
	cd "${_pkgname}"
	make PREFIX=/usr DESTDIR="${pkgdir}" install
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
	#tic -s st.info -o "${pkgdir}/usr/share/terminfo"
}
