# Maintainer: mar77i <mar77i at mar77i dot ch>
# Past Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Scytrin dai Kinthra <scytrin@gmail.com>

pkgname=st-git
_pkgname=st
pkgver=0.7.24.g5a10aca
pkgrel=1
pkgdesc='Simple virtual terminal emulator for X'
url='http://st.suckless.org/'
arch=('i686' 'x86_64')
license=('MIT')
options=('zipman')
depends=('libxft')
makedepends=('ncurses' 'libxext' 'git')
epoch=1
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=('git://git.suckless.org/st#commit=5a10aca702bf7bc9094cb4abaea6c59558740d29'
        'config.h'
        'st-alpha-20160727-308bfbf.diff'
        'st-externalpipe-20170320-e7ed326.diff'
	'st-hidecursor-20170404-745c40f.diff')
md5sums=('SKIP'
         'a339f5567ed2250b74ffb62e8f9037f1'
         '037bb7f2a354fd9e3c15b4aedd103165'
         '102ef3d4141566a406509a6e4b2fa21c'
         '487dc42b84ea464474aac527c2b47191')

pkgver() {
	cd "${_pkgname}"
	git describe --tags |sed 's/-/./g'
}

prepare() {
	cd "${_pkgname}"
	# broken
	#git apply --verbose $srcdir/st-alpha-20160727-308bfbf.diff
	git apply $srcdir/st-externalpipe-20170320-e7ed326.diff
	git apply $srcdir/st-hidecursor-20170404-745c40f.diff
	cp $srcdir/config.h config.h
}

build() {
	cd "${_pkgname}"
	make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
	cd "${_pkgname}"
	make PREFIX=/usr DESTDIR="${pkgdir}" install
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
	tic -s st.info -o "${pkgdir}/usr/share/terminfo"
}
