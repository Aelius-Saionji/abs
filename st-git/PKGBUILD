# Maintainer: Tarmo Heiskanen <turskii@gmail.com>
# Contributor: mar77i <mar77i at mar77i dot ch>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Scytrin dai Kinthra <scytrin@gmail.com>

pkgname=st-git
pkgver=0.8.2.r26.g895e5b5
pkgrel=1
pkgdesc='Simple virtual terminal emulator for X'
url='https://st.suckless.org/'
arch=('i686' 'x86_64')
license=('MIT')
depends=('libxft')
makedepends=('ncurses' 'libxext' 'git')
source=('git://git.suckless.org/st#commit=895e5b50a8cc835c19a45e1e328eb4dc78f5fd0c'
	'config.h'
	'st-externalpipe-0.8.2.diff'
	'st-hidecursor-0.8.1-fixed.diff')
sha1sums=('SKIP'
          '85748ae5157879900e46276eaa37eb294c26962e'
          'cf7530e9926214a3660a938936a8fcaefbeab7c2'
          '5e902a2ea5182658462a541b693bb0e427b7b79f')
provides=('st')
conflicts=('st')

pkgver() {
    cd "${srcdir}/st"
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    cd "${srcdir}/st"

	git apply --reject --ignore-space-change $srcdir/st-externalpipe-0.8.2.diff
	git apply --reject --ignore-space-change $srcdir/st-hidecursor-0.8.1-fixed.diff
	cp $srcdir/config.h config.h
}

build() {
    cd "${srcdir}/st"

    make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
    cd "${srcdir}/st"

    make PREFIX=/usr DESTDIR="${pkgdir}" TERMINFO="/dev/null" install
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    install -Dm644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
}
