# Maintainer: mar77i <mar77i at mar77i dot ch>
# Past Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Scytrin dai Kinthra <scytrin@gmail.com>

pkgname=st-git
_pkgname=st
pkgver=0.7.26.gb331da5
pkgrel=1
pkgdesc='Simple virtual terminal emulator for X'
url='http://st.suckless.org/'
arch=('i686' 'x86_64')
license=('MIT')
options=('zipman')
depends=('libxft')
makedepends=('ncurses' 'libxext' 'git')
epoch=1
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=('git://git.suckless.org/st#commit=b331da550b290d54592b7ba11242c92f5a303a48'
        'config.h'
	'st-alpha-20170509-5a10aca.diff'
        'st-externalpipe-20170513-5a10aca.diff'
	'st-hidecursor-20170404-745c40f.diff')
md5sums=('SKIP'
         '2e0063b091f53258de93f795562beda5'
         'f77d4609a73e60419d3ce05aee112e29'
         'b9599f43d4eb9ab2d99623fd77322ae0'
         '487dc42b84ea464474aac527c2b47191')

pkgver() {
	cd "${_pkgname}"
	git describe --tags |sed 's/-/./g'
}

prepare() {
	cd "${_pkgname}"
	patch -p1 < $srcdir/st-alpha-20170509-5a10aca.diff
	patch -p1 < $srcdir/st-externalpipe-20170513-5a10aca.diff
	patch -p1 < $srcdir/st-hidecursor-20170404-745c40f.diff
	cp $srcdir/config.h config.h
}

build() {
	cd "${_pkgname}"
	make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
	cd "${_pkgname}"
	make PREFIX=/usr DESTDIR="${pkgdir}" install
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
	tic -s st.info -o "${pkgdir}/usr/share/terminfo"
}
