# Maintainer: Tarmo Heiskanen <turskii@gmail.com>
# Contributor: mar77i <mar77i at mar77i dot ch>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Scytrin dai Kinthra <scytrin@gmail.com>

pkgname=st-git
pkgver=0.8.4.r0.gfa253f0
pkgrel=1
pkgdesc='Simple virtual terminal emulator for X'
url='https://st.suckless.org/'
arch=('i686' 'x86_64')
license=('MIT')
depends=('libxft')
makedepends=('ncurses' 'libxext' 'git')
source=('git://git.suckless.org/st#commit=fa253f077f19b3220c7655b81bd91e52f4367803'
	'config.h'
	'st-alphaFocusHighlight-20200216-26cdfeb-fixed.diff'
	'st-externalpipe-0.8.2.diff'
	'st-hidecursor-0.8.1-fixed.diff'
	'st.desktop')
sha1sums=('SKIP'
          '33d0097c85da50507087d7699042c2b91e2feb11'
          '8de7e64982297b05586df8fb0076be6521da5947'
          'cf7530e9926214a3660a938936a8fcaefbeab7c2'
          '7e2b94ae956d057136a3a87d4294d4cee1f0914d'
          'bc7d6fff88c401a5143000f43f3c5fab1d48e974')
provides=('st')
conflicts=('st')

pkgver() {
    cd "${srcdir}/st"
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    cd "${srcdir}/st"

	git apply --reject --ignore-space-change $srcdir/st-externalpipe-0.8.2.diff
	git apply --reject --ignore-space-change $srcdir/st-hidecursor-0.8.1-fixed.diff
	git apply --reject --ignore-space-change $srcdir/st-alphaFocusHighlight-20200216-26cdfeb-fixed.diff
	cp $srcdir/config.h config.h
	cp $srcdir/st.desktop st.desktop
}

build() {
    cd "${srcdir}/st"

    make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
    cd "${srcdir}/st"

    make PREFIX=/usr DESTDIR="${pkgdir}" TERMINFO="/dev/null" install
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    install -Dm644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
	install -Dm644 st.desktop "${pkgdir}/usr/share/applications/st.desktop"
}
