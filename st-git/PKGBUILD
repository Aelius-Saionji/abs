# Maintainer: mar77i <mar77i at mar77i dot ch>
# Past Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Scytrin dai Kinthra <scytrin@gmail.com>

pkgname=st-git
_pkgname=st
pkgver=0.8.2.26.g895e5b5
pkgrel=1
pkgdesc='Simple virtual terminal emulator for X'
url='http://st.suckless.org/'
arch=('i686' 'x86_64')
license=('MIT')
options=('zipman')
depends=('libxft')
makedepends=('ncurses' 'libxext' 'git')
epoch=1
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=('git://git.suckless.org/st#commit=895e5b50a8cc835c19a45e1e328eb4dc78f5fd0c'
	'config.h'
	'st-externalpipe-0.8.2.diff'
	'st-hidecursor-0.8.1-fixed.diff')
md5sums=('SKIP'
         '7f5650b36c38acf32a614e1efbad011b'
         '3a3bcd26bf88f8412e8b5cfa64a83fbc'
         '8d2cdaf7a8ae619a33d17da31fecb5a2')

pkgver() {
	cd "${_pkgname}"
	git describe --tags |sed 's/-/./g'
}

prepare() {
	cd "${_pkgname}"
	git apply --reject --ignore-space-change $srcdir/st-externalpipe-0.8.2.diff
	git apply --reject --ignore-space-change $srcdir/st-hidecursor-0.8.1-fixed.diff
	cp $srcdir/config.h config.h
}

build() {
	cd "${_pkgname}"
	make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
	cd "${_pkgname}"
	make PREFIX=/usr DESTDIR="${pkgdir}" install
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
	#tic -s st.info -o "${pkgdir}/usr/share/terminfo"
}
